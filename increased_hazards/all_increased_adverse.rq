PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX sbdbel: <https://h2020-sbd4nano.github.io/sbdbel/>
PREFIX sio: <http://semanticscience.org/resource/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX aopo: <http://aopkb.org/aop_ontology#>
PREFIX enm: <http://purl.enanomapper.org/onto/>
PREFIX bao: <http://www.bioassayontology.org/bao#>

SELECT DISTINCT ?cause ?relationship ?outcome ?doi_source ?material
WHERE {
  ?causalAssertion a sio:001183 ;                            # Select causal assertions
                   sbdbel:hasCause ?causeIri ;                # Causes
                   sbdbel:hasRelationship ?relationshipIri ;  # Relationships
                   sbdbel:hasOutcome ?outcomeIri ;            # Outcomes
                   sio:000339 ?reference ;                   # Which reference supports the assertion?
                   sio:000332 ?material .                    # To which material does it apply?
  ?reference owl:sameAs ?doi_source .                        # Get DOI from reference
  ?causeIri rdfs:label ?cause .                              # labels
  ?relationshipIri rdfs:label ?relationship .
  ?outcomeIri rdfs:label ?outcome .
  ?outcomeIri rdfs:subClassOf* ?outcomeSuperclassIri .       # Filter for all subCasses of adverse "outcomes"

  
  FILTER ((regex(?relationship, "positively") && (?outcomeSuperclassIri IN ( 
    aopo:AdverseOutcome,
    enm:ENM_0000037,  # Oxidative stress
    bao:BAO_0000201,  # Percent inhibition
    enm:ENM_0000018,  # Toxicological endpoint
    obo:OAE_0000001,  # Adverse event
    obo:GO_1902806,   # Negative regulation of cell cycle
    obo:GO_0051494,   # Negative regulation of cytoskeleton regulation
    obo:GO_1901800    # Positive regulation of proteasomal protein catabolic process
  ))) || (regex(?relationship, "negatively") && (?outcomeSuperclassIri IN ( 
    bao:BAO_0002162,  # Concentration response endpoints (IC, EC, AC, LC)
    enm:NM_0000068,   # Viability
    obo:GO_1903786    # Negative regulation of GSH
  ))))
}
