PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX sbdbel: <https://h2020-sbd4nano.github.io/sbdbel/>
PREFIX sio: <http://semanticscience.org/resource/SIO_>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX cito: <http://purl.org/net/cito/>
PREFIX aopo: <http://aopkb.org/aop_ontology#>
PREFIX enm: <http://purl.enanomapper.org/onto/>
PREFIX bao: <http://www.bioassayontology.org/bao#>
PREFIX npo: <http://purl.bioontology.org/ontology/npo#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX efo: <http://www.ebi.ac.uk/efo/EFO_>
SELECT DISTINCT ?erm ?materialLabel ?cause ?relationship ?outcome ?quote ?doi
WHERE {
    ?causalAssertion a sio:001183 .                           # A causal assertion
    ?causalAssertion sio:000206 ?evidence .                   # Which is supported by an evidence
    ?evidence sbdbel:quote ?quote .                           # Get the quote
    ?evidence cito:containsAssertionFrom/owl:sameAs ?doi .    # Get DOI from evidence
    ?causalAssertion sbdbel:hasCause ?causeIri .              # Get cause
    ?causeIri rdfs:label ?cause .                             # Get cause label
    ?causalAssertion sbdbel:hasRelationship ?relIri .         # Get relationship
    ?relIri rdfs:label ?relationship .                        # Get relationship label
    ?causalAssertion sbdbel:hasOutcome ?outcomeIri .          # Get outcome
    ?outcomeIri rdfs:label ?outcome .                         # Get outcome label
    ?causalAssertion sio:000332 ?material .                   # Get material
    ?material dcterms:identifier ?erm .                       # Get material identifier
    ?material a ?materialType .                               # Get material type
    ?materialType rdfs:label ?materialLabel .                 # Get material type label

    {?materialType rdfs:subClassOf* obo:CHEBI_134441 .         # Ensure material type is subclass of TiO2
    }UNION{
       ?materialType rdfs:subClassOf* npo:NPO_1486 .  
    }
               

    OPTIONAL {
        ?outcomeIri rdfs:subClassOf* ?outcomeSuperclassIri .  # Get outcome superclass
    }
    FILTER (CONTAINS(STR(?doi), "https://doi.org/"))
    FILTER (
        (?relationship = "positively_correlates" && ?outcomeSuperclassIri IN ( 
            aopo:AdverseOutcome,
            enm:ENM_0000037,  # Oxidative stress
            bao:BAO_0000201,  # Percent inhibition
            enm:ENM_0000018,  # Toxicological endpoint
            obo:OAE_0000001,  # Adverse event
            obo:GO_1902806,   # Negative regulation of cell cycle
            obo:GO_0051494,   # Negative regulation of cytoskeleton regulation
            obo:GO_1901800,    # Positive regulation of proteasomal protein catabolic process
            enm:ENM_0000038,   # Uptake
            bao:BAO_0000181,   # Response endpoints (e.g. % inhibition)
          	efo:0004503,		   # hematological measurements
            efo:0004872,		  # inflammatory biomarker measurement
            efo:0801080,   # Oxidative stress biomarker measurement
          	efo:0004352,   # mortality
          	efo:0004873,    # cytokine measurement
          	enm:ENM_0000045,  # transport across barrier
          	enm:ENM_0000038   # uptake

        ))
        ||
        (?relationship = "negatively_correlates" && ?outcomeSuperclassIri IN ( 
            bao:BAO_0002162,  # Concentration response endpoints (IC, EC, AC, LC)
            enm:NM_0000068,   # Viability
            obo:GO_1903786,    # Negative regulation of GSH
          	npo:NPO_1816 	   # % cell viability
          	
        ))
    )
}
