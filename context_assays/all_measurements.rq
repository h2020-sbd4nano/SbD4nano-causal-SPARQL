PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX sbdbel: <https://h2020-sbd4nano.github.io/sbdbel/>
PREFIX sio: <http://semanticscience.org/resource/SIO_>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX cito: <http://purl.org/net/cito/>
PREFIX aopo: <http://aopkb.org/aop_ontology#>
PREFIX enm: <http://purl.enanomapper.org/onto/>
PREFIX bao: <http://www.bioassayontology.org/bao#>
PREFIX npo: <http://purl.bioontology.org/ontology/npo#>
PREFIX dcterms: <http://purl.org/dc/terms/>

SELECT DISTINCT ?assayName ?materialLabel ?organism ?erm ?cause ?relationship ?outcome ?dose ?doseUnits ?endpoint ?endpointMeasurement ?endpointUnits ?quote ?doi
WHERE {
    ## Causal assertion
    ?causalAssertion a sio:001183 ;                           # A causal assertion
                       sio:000206 ?evidence .                 # Which is supported by an evidence
    ?evidence sbdbel:quote ?quote ;                           # Get the quote
              cito:containsAssertionFrom/owl:sameAs ?doi .    # Get DOI from evidence
    ?causalAssertion sbdbel:hasCause ?causeIri .              # Get cause
    ?causeIri rdfs:label ?cause .                             # Get cause label
    ?causalAssertion sbdbel:hasRelationship ?relIri ;         # Get relationship
                     sbdbel:hasOutcome ?outcomeIri .          # Get outcome
    ?relIri rdfs:label ?relationship .                        # Get relationship label

    ?outcomeIri rdfs:label ?outcome .                         # Get outcome label
  	## Material
    ?causalAssertion sio:000332 ?material .                   # Get material
    ?material dcterms:identifier ?erm .                       # Get material identifier
    ?material a ?materialType .                               # Get material type
    ?materialType rdfs:label ?materialLabel .                 # Get material type label                 
	## Assay
  	?evidence obo:ECO_9000001 ?assay .
    ?assay a ?assayType ;
             bao:BAO_0000555 ?assayFormat .
    ?assayType rdfs:label ?assayLabel .
    ?assayFormat rdfs:type ?assayClass .
  	?assayClass rdfs:label ?assayName .
  	## Measure group & Endpoint
  	?assay bao:BAO_0000209 ?mg_iri .
  	?mg_iri bao:BAO_0000208 ?ep_iri .
  	?ep_iri a ?ep .
    ?ep rdfs:label ?endpoint .
  	?ep_iri bao:BAO_0000182 ?endpointUnits .
  	?ep_iri bao:BAO_0000108 ?endpointMeasurement .
  	## Screening concentration
  	?assayFormat bao:BAO_0000338 ?sc_iri .
    ?sc_iri bao:BAO_0002666 ?dose ;
            bao:BAO_0000183 ?doseUnits .
	## Organism
    ?assayFormat bao:BAO_0002921 ?organismIri . 
    ?organismIri rdfs:label ?organism .
FILTER (CONTAINS(STR(?doi), "https://doi.org/"))
}
